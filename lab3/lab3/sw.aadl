package lab3::sw
public
	with lab3::types;

	-- --------------------- PROCESSES AND THREADS ---------------------
	
	-- process and threads for controlling attitude (with implementations)
	process attitudeController -- remember: the connections of the ports of a process with sensors is done in the system implementation
		features
			fss_rot: in data port lab3::types::orientation;
			css_rot: in data port lab3::types::orientation;
			sps_present: in data port lab3::types::sunExist;
			es_rot: in data port lab3::types::orientation;
			mag: in data port lab3::types::orientation;
			rate: in data port lab3::types::angular_rate;
			st_rot: in data port lab3::types::orientation;
			
			telecommand: in data port lab3::types::telecommandString;
			
			tor: out data port lab3::types::torque;
	end attitudeController;
	
	thread fuseOrientation
		features
			fss_rot: in data port lab3::types::orientation;
			css_rot: in data port lab3::types::orientation;
			sps_present: in data port lab3::types::sunExist;
			es_rot: in data port lab3::types::orientation;
			mag: in data port lab3::types::orientation;
			rate: in data port lab3::types::angular_rate;
			st_rot: in data port lab3::types::orientation;
			
			telecommand: in data port lab3::types::telecommandString;
			
			des_orient: out data port lab3::types::orientation;
	end fuseOrientation;
	
	thread implementation fuseOrientation.impl
		properties
			Dispatch_Protocol => Periodic;
			Period => 100ms;
	end fuseOrientation.impl;
	
	
	thread attitudeControlThread
		features
			des_orient: in data port lab3::types::orientation;
			
			tor: out data port lab3::types::torque;
	end attitudeControlThread;
	
	thread implementation attitudeControlThread.impl
		properties
			Dispatch_Protocol => Periodic;
			Period => 100ms;
	end attitudeControlThread.impl;
	
	process implementation attitudeController.impl
		subcomponents
			fusion_worker: thread fuseOrientation;
			act_worker: thread attitudeControlThread;
		connections
			fss_con: port fss_rot -> fusion_worker.fss_rot;
			
			css_con: port css_rot -> fusion_worker.css_rot;
			
			sps_con: port sps_present -> fusion_worker.sps_present;
			
			es_con: port es_rot -> fusion_worker.es_rot;
			
			mag_con: port mag -> fusion_worker.mag;
			
			rate_con: port rate -> fusion_worker.rate;
			
			st_con: port st_rot -> fusion_worker.st_rot;
			
			-- the attitude controller receives the telecommand string as well
			-- in case there are any specific orientation requirements
			telecommand_con: port telecommand -> fusion_worker.telecommand;
			
			
			des_con: port fusion_worker.des_orient -> act_worker.des_orient;
			
			tor_con: port act_worker.tor -> tor;
	end attitudeController.impl;
	
	
	-- process and thread for controlling the orbit (with implementations)
	process orbitController
		features
			gps_pos: in data port lab3::types::position;
			gps_time: in data port lab3::types::gpsTimeStamp;
			
			st_rot: in data port lab3::types::orientation;
			
			telecommand: in data port lab3::types::telecommandString;
			
			dV: out data port lab3::types::delta_v;
	end orbitController;
	
	thread orbitControlThread
		features
			gps_pos: in data port lab3::types::position;
			gps_time: in data port lab3::types::gpsTimeStamp;
			
			st_rot: in data port lab3::types::orientation;
			
			telecommand: in data port lab3::types::telecommandString;
			
			dV: out data port lab3::types::delta_v;
	end orbitControlThread;
	
	thread implementation orbitControlThread.impl
		properties
			Dispatch_Protocol => Periodic;
			Period => 10000ms;
	end orbitControlThread.impl;
	
	process implementation orbitController.impl
		subcomponents
			orbit_worker: thread orbitControlThread;
		connections
			gps_long_con: port gps_pos -> orbit_worker.gps_pos;
			gps_time_con: port gps_time -> orbit_worker.gps_time;
			
			st_x: port st_rot -> orbit_worker.st_rot;
			
			mission_mode_in: port telecommand -> orbit_worker.telecommand;
			
			dv_x_in: port orbit_worker.dV -> dV;
	end orbitController.impl;
	
	
	
	-- process and threads for detecting anomalies and reconfiguring the system (with implementations)
	
	-- this process takes all sensor inputs, compares them and produces on/off signals to (de)activate devices
	process AocsReconfiguration
		features
			-- inputs of the process
			fss_rot: in data port lab3::types::orientation;
			
			css_rot: in data port lab3::types::orientation;
			
			sps_present: in data port lab3::types::sunExist;
			
			es_rot: in data port lab3::types::orientation;
			
			mag: in data port lab3::types::orientation;
			
			rate: in data port lab3::types::angular_rate;
			
			st_rot: in data port lab3::types::orientation;
			
			gps_pos: in data port lab3::types::position;
			gps_time: in data port lab3::types::gpsTimeStamp;
			
			telecommand: in data port lab3::types::telecommandString;
			activate: in data port lab3::types::aocsActivate;
			
			-- outputs of the process
			fss_activate: out data port lab3::types::deviceActivate;
			css_activate: out data port lab3::types::deviceActivate;
			sps_activate: out data port lab3::types::deviceActivate;
			es_activate: out data port lab3::types::deviceActivate;
			mag_activate: out data port lab3::types::deviceActivate;
			rate_activate: out data port lab3::types::deviceActivate;
			st_activate: out data port lab3::types::deviceActivate;
			gps_activate: out data port lab3::types::deviceActivate;
			
	end AocsReconfiguration;
	
	-- this thread is responsible for comparing sensor readings, checking their ranges and issue (de)activation commands for different devices (sensors basically) 
	thread AocsReconfigurationThread
		features
		-- inputs of the thread
			fss_rot: in data port lab3::types::orientation;
			
			css_rot: in data port lab3::types::orientation;
			
			sps_present: in data port lab3::types::sunExist;
			
			es_rot: in data port lab3::types::orientation;
			
			mag: in data port lab3::types::orientation;
			
			rate: in data port lab3::types::angular_rate;
			
			st_rot: in data port lab3::types::orientation;
			
			gps_pos: in data port lab3::types::position;
			gps_time: in data port lab3::types::gpsTimeStamp;
			
			telecommand: in data port lab3::types::telecommandString;
			activate: in data port lab3::types::aocsActivate;
			
			-- outputs of the thread
			fss_activate: out data port lab3::types::deviceActivate;
			css_activate: out data port lab3::types::deviceActivate;
			sps_activate: out data port lab3::types::deviceActivate;
			es_activate: out data port lab3::types::deviceActivate;
			mag_activate: out data port lab3::types::deviceActivate;
			rate_activate: out data port lab3::types::deviceActivate;
			st_activate: out data port lab3::types::deviceActivate;
			gps_activate: out data port lab3::types::deviceActivate;
	end AocsReconfigurationThread;
	
	thread implementation AocsReconfigurationThread.impl
		properties
			Dispatch_Protocol => Periodic;
			Period => 100ms;
	end AocsReconfigurationThread.impl;
	
	
	process implementation AocsReconfiguration.impl
		subcomponents
			worker: thread AocsReconfigurationThread;
		connections
			-- inputs connections
			fss_x: port fss_rot -> worker.fss_rot;
			
			css_x: port css_rot -> worker.css_rot;
			
			sps: port sps_present -> worker.sps_present;
			
			es_x: port es_rot -> worker.es_rot;
			
			magx: port mag -> worker.mag;
			
			rate_con: port rate -> worker.rate;
			
			st_x: port st_rot -> worker.st_rot;
			
			gps_pos_con: port gps_pos -> worker.gps_pos;
			gps_time_con: port gps_time -> worker.gps_time;
	
			telecommand_con: port telecommand -> worker.telecommand;
			activate_cond: port activate -> worker.activate;
			
			-- outputs connections
			fss_act: port worker.fss_activate -> fss_activate;
			css_act: port worker.css_activate -> css_activate;
			sps_act: port worker.sps_activate -> sps_activate;
			es_act: port worker.es_activate -> es_activate;
			mag_act: port worker.mag_activate -> mag_activate;
			rate_act: port worker.rate_activate -> rate_activate;
			st_act: port worker.st_activate -> st_activate;
			gps_act: port worker.gps_activate -> gps_activate;
							
	end AocsReconfiguration.impl;
	
	
	
	-- telemeetry related processes and threads
	process sendTelemetry
		features
			-- inputs of the process
			fss_rot: in data port lab3::types::orientation;
			
			css_rot: in data port lab3::types::orientation;
			
			sps_present: in data port lab3::types::sunExist;
			
			es_rot: in data port lab3::types::orientation;
			
			mag: in data port lab3::types::orientation;
			
			rate: in data port lab3::types::angular_rate;
			
			st_rot: in data port lab3::types::orientation;
			
			gps_pos: in data port lab3::types::position;
			gps_time: in data port lab3::types::gpsTimeStamp;
			
			telemtryMessage: out data port lab3::types::telemetryString;
	end sendTelemetry;
	
	
	thread sendTelemetryThread
		features
			-- inputs of the process
			fss_rot: in data port lab3::types::orientation;
			
			css_rot: in data port lab3::types::orientation;
			
			sps_present: in data port lab3::types::sunExist;
			
			es_rot: in data port lab3::types::orientation;
			
			mag: in data port lab3::types::orientation;
			
			rate: in data port lab3::types::angular_rate;
			
			st_rot: in data port lab3::types::orientation;
			
			gps_pos: in data port lab3::types::position;
			gps_time: in data port lab3::types::gpsTimeStamp;
			
			telemtryMessage: out data port lab3::types::telemetryString;
	end sendTelemetryThread;
	
	thread implementation sendTelemetryThread.impl
		properties
			Dispatch_Protocol => Periodic;
			Period => 1000ms;
	end sendTelemetryThread.impl;
	
	
	process implementation sendTelemetry.impl
		subcomponents
			worker: thread sendTelemetryThread;
		connections
			fss_x: port fss_rot -> worker.fss_rot;
			
			css_x: port css_rot -> worker.css_rot;
			
			sps: port sps_present -> worker.sps_present;
			
			es_x: port es_rot -> worker.es_rot;
			
			magx: port mag -> worker.mag;
			
			ratex: port rate -> worker.rate;
			
			st_x: port st_rot -> worker.st_rot;
			
			gps_pos_con: port gps_pos -> worker.gps_pos;
			gps_time_con: port gps_time -> worker.gps_time;
			
			telemetry_con: port worker.telemtryMessage -> telemtryMessage;
			 
	end sendTelemetry.impl;
	
	
	-- watchdog related processes and threads
	process watchdogEvent
		features
			to_watchdog: out data port lab3::types::watchdogString;
	end watchdogEvent;
	
	thread watchdogEventThread
		features
			to_watchdog: out data port lab3::types::watchdogString;
	end watchdogEventThread;
	
	thread implementation watchdogEventThread.impl
		properties
			Dispatch_Protocol => Periodic;
			Period => 1000ms;
	end watchdogEventThread.impl;
	
	process implementation watchdogEvent.impl --very simple thread that contains a function that generates a periodic event to be sent to watchdog
		subcomponents 
			worker: thread watchdogEventThread;
		connections
			to_watchdog_con: port worker.to_watchdog -> to_watchdog;
	end watchdogEvent.impl;

end lab3::sw;